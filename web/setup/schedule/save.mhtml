<%args>
	$tourn
	$person
	$date => undef
</%args>
<%init>

	my $err;
	my $tz = $tourn->tz;
	$tz = "UTC" unless $tz;

	$m->comp("/funclib/abort.mas",
		warning => "No date sent: $date"
	) unless $date;

	TIMESLOT:
	foreach my $timeslot ($tourn->timeslots) {

		my $name = $ARGS{$timeslot->id."_name"};

		my $ts_date = $ARGS{$timeslot->id."_start_day"};
		$ts_date = $date unless $ts_date;

		my $ts_end_date = $ARGS{$timeslot->id."_end_day"};
		$ts_end_date = $ts_date unless $ts_end_date;

		my $startstring = $ARGS{$timeslot->id."_start"};
		my $endstring = $ARGS{$timeslot->id."_end"};
		next unless ($startstring && $endstring);

	    my $start;
		my $end;

		eval {
			$start = Tab::dtme($ts_date, $ARGS{$timeslot->id."_start"}, $tz);
			$end = Tab::dtme($ts_end_date, $ARGS{$timeslot->id."_end"}, $tz);
		};

		if ($start > $end) {

			my $tname = $timeslot->name;
			$tname =~ s/[^\w\s]//g;
			$err .= " Timeslot ".$tname." not updated:  Started after it ended. <br />";

		} elsif ($start eq $end) {

			my $tname = $timeslot->name;
			$tname =~ s/[^\w\s]//g;
			$err .= " Timeslot ".$tname." not updated:  Started at the same time it ended. <br />";

		} elsif ($start && $end) {

			if (
				$start->epoch ne $timeslot->start->epoch
				|| $end->epoch ne $timeslot->end->epoch
				|| $name ne $timeslot->name()
			) {

				my $description = "Timeslot ".$timeslot->name." times changed: start ".$start." end ".$end;
				my $now = DateTime->now();

				$m->comp("/funclib/log.mas",
					type        => 'tabbing',
					tourn       => $tourn->id,
					person      => $person->id,
					description => $description
				);
			}

			$timeslot->start($start);
			$timeslot->end($end);
			$timeslot->name($name);
			$timeslot->update;

			foreach my $round ( $timeslot->rounds) {
				unless ($round->published > 0) {
					$round->start_time($start);
					$round->update;
				}
			}

		} else {
			$err .= " Timeslot ".$timeslot->name." not updated:  Date string malformed. <br />";
		}
	}

	$m->redirect("/setup/schedule/sked.mhtml?date=".$date."&err=$err");

</%init>
