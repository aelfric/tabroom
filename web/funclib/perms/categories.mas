<%args>
	$perms
	$tourn
	$checker => undef
</%args>
<%init>

	if ($ARGS{"limited"}) {

		my $dbh = Tab::DBI->db_Main();
		my $sth;

		my $query =	"
			select
				category.id, category.name, category.abbr, category.tourn
			from category
			where category.tourn = ?
		";

		if ($perms->{tourn}{$tourn} eq "owner"
			|| $perms->{tourn}{$tourn} eq "tabber"
			|| ($checker && $perms->{tourn}{$tourn} eq "checker")
		) {

			$query .= " order by category.name, category.abbr ";
			$sth = $dbh->prepare($query);

		} else {

			my $ids;

			if ( $perms->{"category"}  && keys %{$perms->{category}}) {
				foreach my $category_id (keys %{$perms->{'category'}}) {

					if (
						($perms->{category}{$category_id} eq "checker" && $checker)
						|| ($perms->{category}{$category_id} eq "tabber")
					) {
						$ids .= ',' if $ids;
						$ids .= int($category_id);
					}
				}
			}

			if ($ids) {
				$query .= "
					and category.id IN ($ids)
					order by category.name, category.abbr";
				$sth = $dbh->prepare($query);
			} else {
				return;
			}
		}

		$sth->execute($tourn->id);
		my $results = $sth->fetchall_hash();
		my %categories;

		foreach my $category (@{$results}) {
			$categories{$category->{id}}{'abbr'}  = $category->{abbr};
			$categories{$category->{id}}{'name'}  = $category->{name};
			$categories{$category->{id}}{'tourn'} = $category->{tourn};
		}

		return %categories;
	}

	if (
		$perms->{tourn}{$tourn} eq "owner"
		|| $perms->{tourn}{$tourn} eq "tabber"
		|| ($checker && $perms->{tourn}{$tourn} eq "checker")
	) {

		return sort {
			$a->abbr cmp $b->abbr
		} $tourn->categories;

	} elsif ( $perms->{"category"}  && keys %{$perms->{category}}) {

		my $ids;

		foreach my $category_id (keys %{$perms->{'category'}}) {
			if (
				($perms->{category}{$category_id} eq "checker" && $checker)
				|| ($perms->{category}{$category_id} eq "tabber")
			) {
				$ids .= ',' if $ids;
				$ids .= int($category_id);
			}
		}

		Tab::Category->set_sql( by_perms => "
			select category.* from category
				where category.id IN ($ids)
			order by category.abbr
		");

		return Tab::Category->search_by_perms();
	}

</%init>
