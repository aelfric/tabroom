<%args>
	$school => undef
</%args>
<%init>

	return unless $school;

	my %school_data = ();
	%school_data = $m->comp("school_entry_data.mas", school => $school);

	%{$school_data{"judging"}} = $m->comp("school_judging_data.mas", school => $school);

	unless (keys %{$school_data{"judging"}}) {
		delete $school_data{"judging"};
	}

	my @fees = $m->comp("/funclib/school_fees.mas",
		school => $school,
		all	=> 1
	);

	$school_data{"fees"}{"amount_due"} = shift @fees;
	my $garbage = shift @fees;

	my $totals = shift @fees;

	$school_data{"fees"}{"entries"}     = $totals->{"entries"};
	$school_data{"fees"}{"fines"}       = $totals->{"fines"};
	$school_data{"fees"}{"bond"}        = $totals->{"bond"};
	$school_data{"fees"}{"concessions"} = $totals->{"concessions"};

	if ($school_data{"fees"}{"amount_due"} < 0) {
		delete $school_data{"fees"}{"amount_due"};
	}

	my $dbh = Tab::DBI->db_Main();

    my $sth = $dbh->prepare("
        select
            person.id person_id, person.first, person.phone, person.no_email,
			contact.onsite, contact.email, contact.official
        from (person, contact)
        where person.id = contact.person
            and contact.school = ?
		group by contact.id
    ");

    $sth->execute($school->id);

	my %ok = ();

	my $contacts = $sth->fetchall_hash();

	foreach my $contact (@{$contacts}) {

		if (
			$contact->{onsite}
			&& $contact->{official}
			&& $contact->{phone}
		) {
			$ok{"onsite_official"}++;
		}

		if ($contact->{phone} && $contact->{official}) {
			$ok{"official"}++;
		}

		if (
			$contact->{onsite}
			&& $contact->{email}
			&& ($contact->{no_email} != 1)
		) {
			$ok{"onsite_email"}++;
		}
	}

	my $contact_errs;

	if (not defined $ok{"onsite_official"}) {
		$contact_errs .= "<br/>" if $contact_errs;
		$contact_errs .= "At least one school contact must be on-site and have an active phone number in Tabroom";
	}

	if (not defined $ok{"onsite_email"}) {
		$contact_errs .= "<br/>" if $contact_errs;
		$contact_errs .= "At least one on-site contact must receive tournament emails";
	}

	if ($ok{"official"} < 2) {
		$contact_errs .= "<br/>" if $contact_errs;
		$contact_errs .= "At least two school contacts must be listed.";
	}

	$school_data{'contacts'} = $contact_errs;

	return %school_data;

</%init>
