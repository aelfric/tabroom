<%args>
	$panel
	$type
</%args>
<%init>

	my $dbh = Tab::DBI->db_Main();

	$type = "ordinals" if $type eq "ndt";

	if ($type eq "ordinals") {

		my $sth = $dbh->prepare("
			select judge.id, round(rating.percentile,1), ballot.side, entry.id entry
				from judge, rating, ballot, entry, event
			where judge.id = rating.judge
				and event.id = entry.event
				and entry.id = ballot.entry
				and ballot.panel = ?
				and rating.entry = entry.id
				and rating.rating_tier < 1
				and (ballot.side = rating.side
						OR rating.side = 0
						OR rating.side IS NULL
					)
				and not exists (
					select distinct noprefs.id
					from school_setting noprefs
					where noprefs.tag = 'noprefs'
					and noprefs.value = 1
					and noprefs.school = entry.school
				)
		");

		my %ratings;
		my @judges;

		$sth->execute($panel->id);

		my $aff;
		my $neg;

		while (my ($id, $name, $side, $entry) = $sth->fetchrow_array() ) {

			if ((not defined $aff) && $side == 1) {
				$aff = $entry;
			}

			if ((not defined $neg) && $side == 2) {
				$neg = $entry;
			}

			$ratings{$entry}{$id} = $name;
			push @judges, $id;
		}

		my %seen = ();
		@judges = grep { ! $seen{$_} ++ } @judges;

		my %mutuality;

		foreach my $judge (@judges) {
			$mutuality{$judge} = abs($ratings{$aff}{$judge} - $ratings{$neg}{$judge});
			$mutuality{$judge} = 100 unless $mutuality{$judge};
		}

		return \%ratings, \%mutuality;

	} else {

		my $sth = $dbh->prepare("
			select judge.id, rating_tier.name, ballot.side, rating_tier.strike
			from judge, rating, rating_tier, ballot, entry, event
			where judge.id = rating.judge
			and rating.rating_tier = rating_tier.id
			and event.id = entry.event
			and entry.id = ballot.entry
			and ballot.panel = ?
			and rating.entry = entry.id
			and not exists (
				select distinct noprefs.id
				from school_setting noprefs
				where noprefs.tag = 'noprefs'
				and noprefs.value = 1
				and noprefs.school = entry.school
			)
		");

		$sth->execute($panel->id);

		my %aff_rating;
		my %neg_rating;
		my %struck;

		my @judges;

		while (my ($id, $name, $side, $strike) = $sth->fetchrow_array() ) {
			$aff_rating{$id} = $name if $side == 1;
			$neg_rating{$id} = $name if $side == 2;
			$struck{$id}++ if $strike;
			push @judges, $id;
		}

		my %seen = ();
		@judges = grep { ! $seen{$_} ++ } @judges;

		my %mutuality;

		foreach my $judge (@judges) {
			$mutuality{$judge} = abs($aff_rating{$judge} - $neg_rating{$judge});
			$mutuality{$judge} = 100 unless $aff_rating{$judge} && $neg_rating{$judge};
			$mutuality{$judge} = 100 if $struck{$judge};
		}

		return \%aff_rating, \%neg_rating, \%mutuality;

	}

</%init>
