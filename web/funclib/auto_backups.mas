<%args>
	$round
</%args>
<%init>

	return unless $round;

	my $event = $round->event;
	my $tourn = $event->tourn;
	my $follower_ids = $event->setting("backup_people");
	my $tourn_followers = $tourn->setting("backup_followers");

	return unless ($follower_ids || $tourn_followers);

	my $owner = $tourn->permissions(tag => "owner")->first;
    return unless $owner;

	$follower_ids =~ s/^,//;

	my @followers;

	if ($follower_ids) {
		@followers = split(/,/, $follower_ids);
	}

	if ($tourn_followers) {
		push @followers, split(/,/, $tourn_followers);
	}

	return unless @followers;

	my %seen;
	@followers = grep { ! $seen{$_} ++ } @followers;

	my $results_ref = $m->comp("/api/download_data.mhtml",
		round_id  => $round->id,
		person_id => $owner->person->id,
		tourn_id  => $round->event->tourn->id,
		file      => 1
	);

	my ($filepath, $filename) = @{$results_ref};

	my $now = DateTime->now(
		time_zone => $event->tourn->tz
	);

	my $body = "\n\nBackup of ".$round->realname." taken at ".Tab::nicedt($now)."\n\n";

	$body .= "To restore, upload this file on the Schematics screen for the round, ";
	$body .= "under the blue Settings button on top center\n";
	$body .= "Event ".$event->name."\n";
	$body .= "Tournament ".$event->tourn->name."\n";

	my $round_name = $round->realname;
	my $event_name = $event->abbr;
	my $subject = $ARGS{"subject"};

	$round_name =~ s/[\W_]//g;
	$event_name =~ s/[\W_]//g;

	my $subject_line = "$event_name $round_name Backup $subject";

	foreach my $id (@followers) {

		next unless $id;
		my $person = Tab::Person->retrieve($id);
		next unless $person;
		my $to = $person;

		$m->comp( "/funclib/send_email.mas",
			from_string     => 'Tabroom Backups <backups@www.tabroom.com>',
			to              => $to,
			subject         => $subject_line,
			body            => $body,
			attachment_name => $filename,
			attachment_path => $filepath,
			admin           => 1
		);
	}

	return;

</%init>
