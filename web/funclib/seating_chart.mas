<%args>
	$dbh   => undef
	$panel => undef
	$person => undef
</%args>
<%init>

	return unless $panel;

	unless ($dbh) {
		$dbh = Tab::DBI->db_Main();
	}

	my $sth = $dbh->prepare("
		select
			entry.id, entry.code, entry.name, ballot.seat rowseat, entry.event, student.person
		from ballot, entry, entry_student es, student
			where ballot.panel = ?
			and ballot.entry = entry.id
			and ballot.seat IS NOT NULL
			and entry.id = es.entry
			and es.student = student.id
		group by entry.id
			order by ballot.seat
	");

	$sth->execute($panel->id);

	my $seats = $sth->fetchall_hash();

	$sth->finish();

	my $event = $panel->round->event;

	my $max_row;
	my $max_seat;
	my %by_seat;

	foreach my $seat_ref (@{$seats}) {
		($seat_ref->{row}, $seat_ref->{seat}) = split (/\-/, $seat_ref->{"rowseat"});
		$max_row = $seat_ref->{"row"} if $max_row < $seat_ref->{"row"};
		$max_seat = $seat_ref->{"seat"} if $max_seat < $seat_ref->{"seat"};
		$by_seat{$seat_ref->{"row"}}{$seat_ref->{"seat"}} = $seat_ref;

		unless ($event) {
			$event = Tab::Event->retrieve($seat_ref->{'event'});
		}
	}

	unless ($event) {
		$m->comp("/funclib/abort.mas", message => "No event found for that chamber");
	}

	my %event_settings = $event->all_settings();

	my @rows = (1 .. $max_row);

	if ($ARGS{"chair"}) {
		@rows = reverse @rows;
	}

	my $yup;

	if ($event_settings{"congress_seating_entrycodes"}
		|| $event_settings{"congress_seating_entrynames"}
		|| $event_settings{"congress_seating_schoolcodes"}
		|| $event_settings{"congress_seating_schoolnames"}
	) {
		$yup++;
	}

</%init>


%	if ($yup) {
		<h5 class="centeralign martopmore marbottommore">
			<% $ARGS{"chair"} ? "Back of Room" : "Front of Room" %>
		</h5>
%	} else {
		<h5 class="centeralign martopmore marbottommore redtext">
			Seating Charts are not configured for this tournament
		</h5>
%	}

	<table>
%		foreach my $row (@rows) {
			<tr class="row">

<%perl>

			my $seat = 1;

			if ($ARGS{"chair"}) {
				$seat = $max_seat;
			}

			while ($seat && ($seat < $max_seat + 1))  {
				my $entry = $by_seat{$row}{$seat};

				if ($ARGS{"chair"}) {
					$seat--;
				} else {
					$seat++;
				}
</%perl>
				<td class="padvertmore padleft padright
					<% $entry && $entry->{'person'} == $person ? "italic semibold redtext" : "" %>"
				>
					<span class="halfspacer marvertmore"></span>

%					if ($entry) {
%						if ($event_settings{"congress_seating_entrycodes"}) {
							<% $entry->{"code"} %>
%						}
%						if ($event_settings{"congress_seating_entrynames"}) {
							<% $entry->{"name"} %>
%						}
%						if ($event_settings{"congress_seating_schoolcodes"}) {
							<% $entry->{"school_code"} %>
%						}
%						if ($event_settings{"congress_seating_schoolnames"}) {
							<% $entry->{"school_name"} %>
%						}
%					}
				</td>
%			}
			</tr>
%		}
	</table>

%	if ($yup) {
		<h5 class="centeralign martopmuchmore marbottommore">
			<% $ARGS{"chair"} ? "Front of Room" : "Back of Room" %>
		</h5>
%	}
