<%args>
	$school_id
	$person
</%args>
<%init>

	my $school = Tab::School->retrieve($school_id);

	unless ($school && $school->id) {
		my $err = "School is not defined.  Perhaps you already deleted it?";
		$m->redirect("/user/home.mhtml?err=$err");
	}

	unless (
		Tab::Permission->search(
			tag     => "chapter",
			chapter => $school->chapter,
			person  => $person->id
		)
	) {
		my $err = "You are not an administrator for that school and cannot delete it";
		$m->redirect("/user/home.mhtml?err=$err");
	}

	my ($fee, $feline_ref) =
		$m->comp(
			"/funclib/school_fees.mas",
			school     => $school,
			entry_fees => 1
		);

	if ($fee < 0) {
		my $err = "Your school is owed a refund by this tournament and deleting your school ";
		$err .= "would delete all record of that.";
		$m->redirect("/user/home.mhtml?err=$err");
	}

	my ($payments) =
		$m->comp(
			"/funclib/school_fees.mas",
			school   => $school,
			payments => 1
		);

	if ($payments < 0) {
		my $err = "Your school has registered payments already and dropping the school would drop all record of that.  Therefore it is not allowed";
		$m->redirect("/user/home.mhtml?err=$err");
	}

	my $tourn = $school->tourn;

	foreach my $entry ($school->entries) {
		foreach my $strike (Tab::Strike->search(entry => $entry->id)) { $strike->delete;}
		$entry->delete();
	}

	foreach my $judge ($school->judges) {

		foreach my $strike ($judge->strikes) {
			$strike->delete();
		}
		$judge->delete();
	}

   if ($tourn->setting("track_reg_changes")) {

        my $description = $person->first." ".$person->last." dropped the entire entry for ".$school->name.".  Bummer.";

        my $change = $m->comp("/funclib/log.mas",
            tourn       => $tourn->id,
	        type        => "registration",
	        description => $description,
			person      => $person->id
        );

   	}

	Tab::log($person->first." ".$person->last." deleted school ".$school->name." from ".$tourn->name);

	$school->delete;

	my $msg = "You have been dropped completely from ".$tourn->name.".  I warned you.";
	$m->redirect("/user/home.mhtml?msg=$msg");

</%init>

