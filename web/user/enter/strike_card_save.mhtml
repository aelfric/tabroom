<%args>
	$person
	$panel_id  => undef
	$entry_id  => undef
</%args>
<%init>

	my $panel = Tab::Panel->retrieve($panel_id) if $panel_id;
	my $entry = Tab::Entry->retrieve($entry_id) if $entry_id;

	unless ($panel > 0) {
		$m->comp('/funclib/abort.mas', message => "No valid section found for ID $panel_id");
	}

	unless ($entry > 0) {
		$m->comp('/funclib/abort.mas', message => "No valid entry found for ID $entry_id");
	}

	my $round = $panel->round;
	my $event = $round->event;

	unless ($round->setting('strikes_published')) {
		my $err = "That strike card is no longer available";
		$m->redirect("strike_cards.mhtml?err=$err");
	}

	my $now = DateTime->now();
	my $deadline = $round->setting('strikes_due');

	if ($deadline < $now) {
		my $err = "The deadline to fill out that strike card has passed";
		$m->redirect("strike_cards.mhtml?err=$err");
	}

	my $limit = $round->setting('strikes');

	my $msg;

	Tab::Score->set_sql( zero => "
		delete score.*
			from score
		where score.tag in ('strike', 'no_strike')
			and score.ballot = ?
	");

	if ($entry) {

		foreach my $ballot ($panel->ballots( entry => $entry->id)) {

			Tab::Score->sql_zero->execute($ballot->id);
			my $judge = $ballot->judge;
			next unless $judge;

			if ($limit > 0 && $ARGS{$ballot->id}) {

				$msg .= "<br />" if $msg;
				$msg .= $judge->first." ".$judge->last." struck for ".$entry->code;

				Tab::Score->create({
					ballot => $ballot->id,
					tag    => "strike",
					value  => 1
				});

				$limit--;
			}
		}

		if ($limit > 0 && $ARGS{$panel->id."_".$entry->id."_nope"}) {
			$limit = 0;
			my $ballot = $panel->ballots(entry => $entry->id)->first;

			$msg .= "<br />" if $msg;
			$msg .= "Balance of strikes were refused";

			Tab::Score->create({
				ballot => $ballot->id,
				tag    => "no_strike",
				value  => 1
			});
		}
	}

	my $err;

	if ($limit) {
		$err .= "You still have ".$limit." strike(s) remaining.  Please select or choose No Strikes";
	}

	if ($msg) {

		$m->comp("/funclib/log.mas",
			type        => "strikecard",
			school      => $entry->school,
			entry       => $entry,
			event       => $event,
			category    => $event->category->id,
			description => $msg,
			panel       => $panel->id,
			person      => $person->id
		);
	}

	$m->redirect("strike_cards.mhtml?err=$err&msg=$msg");

</%init>
