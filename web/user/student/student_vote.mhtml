<%args>
	$person
	$entry_id => undef
	$panel_id => undef
</%args>
<%init>

	my $chamber = Tab::Panel->retrieve($panel_id) if $panel_id;
	my $voter = Tab::Entry->retrieve($entry_id) if $entry_id;

	unless ($chamber && $voter) {
		my $err = "No voter found for ID $entry_id or no chamber found for $panel_id";
		$m->redirect("/user/student/index.mhtml?err=$err");
	}

	my $student = $voter->students->first;

	unless ($student->person == $person || $person->site_admin) {
		my $err = "Your account does not have access to that ballot record";
		$m->redirect("/user/student/index.mhtml?err=$err");
	}

	my $round = $chamber->round;

	unless ($round->setting('student_vote_open')) {
		my $err = "That round is not open for voting";
		$m->redirect("/user/student/index.mhtml?err=$err");
	}

	my $event = $round->event;
	my $tourn = $event->tourn;

	my %votes;

	my $truncate;
	$truncate = $event->setting("student_truncate_fill") if $event;

	my $dbh = Tab::DBI->db_Main();


</%init>

	<div class="main">

<%perl>
		my $vote_chamber;

		my $leadership = $event->setting("leadership_protocol");

		my %tb_types;

		if ($leadership) {
			%tb_types = $m->comp(
				"/funclib/tiebreak_types.mas",
				protocol_id => $leadership,
				round => $round
			);

		} else {
			%tb_types = $m->comp(
				"/funclib/tiebreak_types.mas",
				round => $round
			);
		}


		if ($event->setting("po_contest")) {

			my $po = Tab::Event->retrieve($event->setting("po_contest"));

			if ($po) {

				my $vote_session = $po->rounds(name => $round->name)->first;

				if ($vote_session) {
					$vote_chamber = $vote_session->panels(letter => $chamber->letter)->first;

					if ($vote_chamber) {
						%tb_types = $m->comp(
							"/funclib/tiebreak_types.mas",
							round => $vote_session
						);
					}
				}
			}
		}

		unless ($vote_chamber) {
			$vote_chamber = $chamber;
		}

		my $nom_sth = $dbh->prepare("
			select
				vote.id, vote.entry, vote.tag, vote.value
				from (student_vote vote)
			where vote.panel = ?
			and vote.tag = 'nominee'
		");

		$nom_sth->execute($chamber->id);

		my %noms;
		my $limited;

		while (
			my (
				$id, $entry, $tag, $value
			)  = $nom_sth->fetchrow_array()
		) {
			$noms{$entry} = $value;
			$limited++;
		}

		$nom_sth->finish();
		my $votes;

		if ($limited) {
			$votes = $limited;
			$votes = $truncate if (defined $truncate) && $truncate < $limited;
		} else {
			$votes = $truncate if (defined $truncate);
		}

		my $sth = $dbh->prepare("
			select voter.id, vote.entry, vote.id, vote.tag, vote.value
				from (student_vote vote, entry voter)

				left join entry_student es on es.entry = voter.id
				left join student on es.student = student.id
			where vote.panel = ?
			and vote.voter = ?
			and vote.voter = voter.id
		");

		$sth->execute($vote_chamber->id, $voter->id);

		my %ballot;

		while (
			my (
				$voter_id, $entry_id, $id, $tag, $value
			) = $sth->fetchrow_array()
		) {
			$ballot{$entry_id} = $value;
		}

		$sth->finish();

</%perl>
		<form
			action = "student_vote_save.mhtml"
			method = "post"
		>

		<input
			type  = "hidden"
			name  = "panel_id"
			value = "<% $chamber->id %>"
		>

		<input
			type  = "hidden"
			name  = "voter_id"
			value = "<% $voter->id %>"
		>

			<div class="full">
				<span class="third">
					<h4 class="nospace">
						Competitor Vote
					</h4>
				</span>

				<span class="third centeralign">
					<h5 class="nospace">
						<% $event->abbr %> Chamber <% $chamber->letter %>
					</h5>
				</span>

				<span class="third rightalign">
					<h4 class="semibold nospace">
						<% $voter->code %>
					</h4>
				</span>
			</div>

%			if ($tb_types{"entry_rank"}) {
				<span class="full semibold redtext centeralign bigger italic odd marbottommore padvertmore">
%					if ($truncate) {
						Rank the best entries 1 - <% $truncate - 1 %>, leave any others blank. No ties.
%					} else {
						Rank all entries in order, 1 being best, without any ties.
%					}
				</span>
%			}
%			if ($tb_types{"entry_winloss"}) {
				<span class="full semibold redtext centeralign bigger italic odd marbottommore padvertmore">
					Choose ONE winner
				</span>
%			}

			<div class="yellowrow smallish semibold flexrow">
				<span class="quarter blueborderright padleftmore padtopmore fixedheight">
					Competitor
				</span>

				<span class="threequarters nospace padleftmore padtopmore fixedheight">
%					if ($tb_types{"entry_winloss"}) {
						Winner
%					} else {
						Rank
%					}
				</span>
			</div>
<%perl>

			my @entries = $m->comp("/funclib/panel_entries.mas", panel => $vote_chamber);
			my $panel_size = scalar @entries;
			my $counter = 1;

			foreach my $entry (@entries) {

				next if $limited && (not defined $noms{$entry->id});
</%perl>
				<div class="row flexrow nospace">

					<span class="quarter blueborderright semibold bluetext padleftmore padtopmore fixedheight marno">
						<% $entry->code %>
						<% $entry->code ne $entry->name ? $entry->name : "" %>
					</span>

					<span class="threequarters padleft flexrow fixedheight marno">

%						if ($tb_types{"entry_winloss"}) {
							<span class="marno quarter centeralign hover grow">
								<label for="<% $entry->id %>_win">
									<input
										type  = "radio"
										name  = "winner"
										id    = "<% $entry->id %>_win"
										value = "<% $entry->id %>"
										<% $ballot{$entry->id} == 1 ? "checked" : "" %>
									>
								</label>
							</span>
%						}

%						if ($tb_types{"entry_rank"}) {
%							if ($votes && $votes < 13) {
%								foreach my $vote (1 .. $votes) {
									<span class="marno tenth hover grow padleft">
										<label for="<% $entry->id %>_<% $vote %>">
											<% $vote %>
											<input
												type  = "radio"
												class = "padleft"
												name  = "<% $entry->id %>"
												id    = "<% $entry->id %>_<% $vote %>"
												value = "<% $vote %>"
												<% $ballot{$entry->id} == $vote ? "checked" : "" %>
											>
										</label>
									</span>
%								}
%							} else {
								<span class="hidden">
									<% $ballot{$entry->id} ? $ballot{$entry->id} : "-1" %>
								</span>

								<input
									type          = "number"
									tabindex      = "<% $counter++ %>"
									name          = "<% $entry->id %>"
									value         = "<% $ballot{$entry->id} != $truncate ? $ballot{$entry->id} : ""%>"
									min 		  = 1
									max           = "<% $truncate ? $truncate : $panel_size %>"
								>
%							}
%						}
					</span>
				</div>
%			}

			<div class="liblrow rightalign">
				<span class="quarter centeralign">
					<input type="submit" value="Save Ballot">
				</span>
			</div>
		</form>

	</div>

	<div class="menu">
		<div class="sidenote">
			<h4>Return</h4>

			<a
				class="full blue"
				href="/user/student/index.mhtml"
			>Return to <% $person->first %> <% $person->last %>
			</a>

		</div>
	</div>
