<%args>
	$id    => undef
	$key   => undef
	$pass1 => undef
	$pass2 => undef
</%args>
<%init>

	$m->abort unless $id;
	$m->abort unless $key;
	$m->abort unless $pass1;
	$m->abort unless $pass2;

	my $now = DateTime->now();
	my $target = Tab::Person->retrieve($id);

	unless ($target) {
		$m->comp("/funclib/abort.mas", message => "Invalid account ID sent");
	}

	my %target_settings = $target->all_settings();
	my $err;

	unless ($target_settings{"pass_change_expires"} && $target_settings{"pass_changekey"}) {

		$err = "No valid password change key found";
		$target->setting("pass_change_expires", "");
		$target->setting("pass_changekey", "");

	} elsif ($target_settings{"pass_change_expires"}
		&& $target_settings{"pass_change_expires"}->epoch < $now->epoch
	) {

		$err = "No valid password change key found";
		$target->setting("pass_change_expires", "");
		$target->setting("pass_changekey", "");

	} elsif ($target_settings{"pass_changekey"} != $key) {

		$err = "No valid password change key found";
		$target->setting("pass_change_expires", "");
		$target->setting("pass_changekey", "");

	} elsif ($pass1 ne $pass2) {

		$err = "The two passwords you entered did not match";
	}

	$m->redirect("/user/login/forgot.mhtml?err=$err") if $err;

	my $salt = $m->comp("/funclib/generate_randomstring.mas");
	my $new_sha2_hash = crypt($pass1, '$6$' . $salt);

	$target->password($new_sha2_hash);
	$target->update();

	$target->setting("pass_change_expires", "");
	$target->setting("pass_changekey", "");
	$target->setting("pass_timestamp", "date", $now);

	Tab::log(
		"PASSWORD CHANGE: Person ID ".$target->id." (".$target->email.") "
		.$target->first." ".$target->last.
		" password was changed from IP address ".$ENV{REMOTE_ADDR}.
		" using a forgotten password link "
	);

	my $msg = "Your password has been changed.  You may now log in.";

	my $subject = "Your Tabroom.com password has been changed";
	my $email = "\n\tHello! \n";

	$email .= "The password for your Tabroom.com account has been changed ";
	$email .= "If you did not attempt to make this change, please notify us at ";
	$email .= 'help@tabroom.com immediately. ';

	my @raw;

	if ($target->phone && $target->provider) {

		push @raw, $target->phone.'@'.$target->provider;

		$m->comp("/funclib/send_notify.mas",
			array     => \@raw,
			subject => $subject,
			body    => $email
		);
	}

	$email .= "\n\n";

	$email .= "Thank you!\n";
	$email .= "Tabroom.com, a project of the National Speech & Debate Assocation\n";
	$email .= "https://www.tabroom.com\n";
	$email .= "https://www.speechanddebate.org\n";

	undef @raw;
	push @raw, $target->email;

	$m->comp("/funclib/send_email.mas",
		raw     => \@raw,
		subject => $subject,
		body    => $email
	);

	$m->redirect("/user/login/login.mhtml?username=".$target->email."&msg=$msg");

</%init>

