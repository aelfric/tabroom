<%args>
	$tourn
	$tourn_settings
</%args>
<%init>

	my $dbh = Tab::DBI->db_Main();

	my $sth = $dbh->prepare("
		select
			school.id,
			school.region,
			person.id person
		from school, permission, person

		where school.tourn = ?
			and school.chapter = permission.chapter
			and permission.person = person.id
	");

	$sth->execute($tourn->id);

	my $regions = $sth->fetchall_hash();

	my %region_people;

	foreach my $reg (@{$regions}) {
		$region_people{$reg->{"region"}}{$reg->{person}}++;
	}

	my $student_sth = $dbh->prepare("
		select
			school.region, student.person

		from school, entry, entry_student es, student
			where school.tourn = ?
			and school.id = entry.school
			and entry.id = es.entry
			and es.student = student.id
			and student.person > 0
	");

	$student_sth->execute($tourn->id);
	my $students = $student_sth->fetchall_hash();

	foreach my $student (@{$students}) {
		$region_people{$student->{region}}{$student->{person}}++;
	}


	my $entry_sth = $dbh->prepare("
		select
			entry.id, school.region
		from entry, school
			where school.tourn = ?
			and school.id = entry.school
	");

	$entry_sth->execute($tourn->id);
	my $entries = $entry_sth->fetchall_hash();

	my $follow_sth = $dbh->prepare("insert into follower (type, person, tourn, entry) VALUES ('entry', ?, ?, ?) ");
	my $delete_sth = $dbh->prepare("delete from follower where entry = ? and person = ? ");

	foreach my $entry (@{$entries}) {
		foreach my $person (keys %{$region_people{$entry->{region}}}) {
			$delete_sth->execute($entry->{id}, $person);
			$follow_sth->execute($person, $tourn, $entry->{id});
		}
	}

	my $message = "Followers regionalized";

	$m->redirect("index.mhtml?msg=$message");

</%init>
