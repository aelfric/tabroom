<%args>
	$category_id
	$tourn
	$tourn_settings
	$only_category => undef
</%args>
<%init>

	my $tz = $tourn->tz;
	$tz = "UTC" unless $tz;

	my $category = Tab::Category->retrieve($category_id);
	$category = $only_category if $only_category;

	my $rounds_per = $category->setting("rounds_per");

	my @category_timeslots = $m->comp(
		"/funclib/category_timeslots.mas",
		category => $category
	);

	my %school_entries = $m->comp(
		"/funclib/school_entry_by_category.mas",
		category => $category,
		all      => 1
	);

	my %strikes;

	my $dbh = Tab::DBI->db_Main();
	my $strikes_sth = $dbh->prepare("

		select judge.id, judge.code, judge.first, judge.last, judge.active,
			judge.school, judgeschool.name,
			judge.obligation, judge.hired,
			strike.id, strike.type, strike.conflict, strike.conflictee, strike.registrant, strike.created_at,
			strike.start, strike.end,
			entry.id, entry.code,
			school.id, school.code, school.name,
			person.id, person.email, person.first, person.last

		from (judge, strike)

		left join entry on strike.entry = entry.id
		left join school on strike.school = school.id
		left join school judgeschool on judge.school = judgeschool.id
		left join person on person.id = strike.entered_by

		where judge.category = ?
		and judge.id = strike.judge
	");

	$strikes_sth->execute($category);

	while (
		my (
			$judge_id, $judge_code, $judge_first, $judge_last, $judge_active,
			$judge_school, $judge_schoolname,
			$judge_obligation, $judge_hired,
			$strike_id, $strike_type, $strike_conflict, $strike_conflictee, $strike_registrant, $strike_created_at,
			$strike_start, $strike_end,
			$entry_id, $entry_code,
			$school_id, $school_code, $school_name,
			$person_id, $person_email, $person_first, $person_last
		)  = $strikes_sth->fetchrow_array()
	) {

		$strikes{$judge_id}{"code"} = $judge_code;
		$strikes{$judge_id}{"name"} = $judge_first." ".$judge_last;
		$strikes{$judge_id}{"last"} = $judge_last;
		$strikes{$judge_id}{"active"} = $judge_active;
		$strikes{$judge_id}{"obligation"} = $judge_obligation + $judge_hired;

		if ($judge_school) {
			$strikes{$judge_id}{"school"}{$judge_school}{"Own"}++;
		}

		next if $school_id == $judge_school;

		$strikes{$judge_id}{$strike_id}{"tag"} = "Strike";
		$strikes{$judge_id}{$strike_id}{"tag"} = "Conflict" if $strike_conflict;

		$strikes{$judge_id}{$strike_id}{"entered_name"}  = $person_first." ".$person_last;
		$strikes{$judge_id}{$strike_id}{"entered_email"} = $person_email;
		$strikes{$judge_id}{$strike_id}{"created_at"}    = $strike_created_at;
		$strikes{$judge_id}{$strike_id}{"conflictee"}    = $strike_conflictee;

		if ($entry_id) {

			$strikes{$judge_id}{$strike_id}{"id"}         = $entry_id;
			$strikes{$judge_id}{$strike_id}{"target"}     = "entry";
			$strikes{$judge_id}{$strike_id}{"entry_code"} = $entry_code;
			$strikes{$judge_id}{"entries"}{$entry_id}{$strike_type} = $entry_code;

		} elsif ($school_id) {

			$strikes{$judge_id}{"schools"}{$school_id}{$strike_type} = $school_name;

		} elsif ($strike_start) {
			eval {
				$strikes{$judge_id}{"times"}{$strike_id}{"start"} = DateTime::Format::MySQL->parse_datetime($strike_start);
				$strikes{$judge_id}{"times"}{$strike_id}{"end"} = DateTime::Format::MySQL->parse_datetime($strike_end);
				$strikes{$judge_id}{"times"}{$strike_id}{"start"}->set_time_zone("UTC");
				$strikes{$judge_id}{"times"}{$strike_id}{"end"}->set_time_zone("UTC");
			};
		}
	}

	my $sth = $dbh->prepare("
		select judge.id, panel.id, round.type
			from judge, ballot, panel, round
		where judge.category = ?
			and judge.id = ballot.judge
			and ballot.panel = panel.id
			and panel.round = round.id
		group by panel.id
	");

	$sth->execute($category->id);
	my %panels;

	while (
		my (
			$judge_id, $panel_id, $round_type
		)  = $sth->fetchrow_array()
	) {

		$panels{$judge_id}{"all"}{$panel_id} = $round_type;

		if ($round_type eq "elim" || $round_type eq "final") {
			$panels{$judge_id}{"elim"}++;
		} else {
			$panels{$judge_id}{"prelim"}++;
		}
	}


</%init>

	<& menu.mas,
		only_category  => $only_category,
		tourn          => $tourn,
		tourn_settings => $tourn_settings,
		whoami         => "conflicts",
		category       => $category
	&>

	<div class="main">

		<span class="threequarters">
			<h4><% $category->abbr %> Conflicts</h4>
		</span>

		<span
			class = "quarter rightalign"
			id    = "conflicts_buttonarea"
		>
		</span>

		<& "/funclib/tablesorter.mas", table => "conflicts" &>

		<table id="conflicts">

			<thead>
				<tr class="yellowrow padvert smallish">

					<th class="padvert">
						JCode
					</th>

					<th class="padvert">
						Judge
					</th>

					<th>
						# Conflicts
					</th>

					<th>
						Obl.
					</th>

					<th>
						Judged
					</th>

					<th>
						Prelim
					</th>

					<th>
						Elim
					</th>

					<th class="noshort">
						List
					</th>

				</tr>

			</thead>

			<tbody>
<%perl>

				foreach my $judge_id (
					sort {$strikes{$a}{"name"} cmp $strikes{$b}{"name"}}
					keys %strikes
				) {

					my %all_entries = map {$_ => 1}  (keys %{$strikes{$judge_id}{"entries"}});

					foreach my $school_id ( keys %{$strikes{$judge_id}{"schools"}}) {
						foreach my $entry_id (keys %{$school_entries{$school_id}}) {
							$all_entries{$entry_id}++;
						}
					}
</%perl>

					<tr>

						<td class="nospace">
							<a
								class="white full padvert padleft more"
								href="/register/judge/judge_strikes.mhtml?judge_id=<% $judge_id %>"
							>
								<% $strikes{$judge_id}{"code"} %>
							</a>
						</td>
						<td class="nospace">
							<a
								class="white full padvert padleft more"
								href="/register/judge/judge_strikes.mhtml?judge_id=<% $judge_id %>"
							>
								<% $strikes{$judge_id}{"name"} %>
							</a>
						</td>

						<td class="rightalign">
							<% scalar( keys %all_entries ) %>
						</td>

						<td class="rightalign">
							<% $strikes{$judge_id}{"active"}
								? $strikes{$judge_id}{"obligation"}
								: "Inactive"
							%>
						</td>

						<td class="rightalign">
							<% scalar (keys %{$panels{$judge_id}{"all"}}) %>
						</td>
						<td class="rightalign">
							<% $panels{$judge_id}{"prelim"} ?  $panels{$judge_id}{"prelim"}  : 0 %>
						</td>

						<td class="rightalign">
							<% $panels{$judge_id}{"elim"} ?  $panels{$judge_id}{"elim"}  : 0 %>
						</td>

						<td class="smaller nospace">
<%perl>
							foreach my $school_id (
								sort keys %{$strikes{$judge_id}{"schools"}}
							) {

								foreach my $type (
									sort {$a cmp $b} (keys %{$strikes{$judge_id}{"schools"}{$school_id}})
								) {
</%perl>

									<div class="full marno padless">
										<span class="third semibold bluetext nospace">
											<% ucfirst($type) %>
										</span>
										<span class="twothirds nospace">
											(<% scalar (keys %{$school_entries{$school_id}}) %>)
											<% $strikes{$judge_id}{"schools"}{$school_id}{$type} %>
										</span>
									</div>
<%perl>
								}
							}

							foreach my $entry_id (
								sort keys %{$strikes{$judge_id}{"entries"}}
							) {
								foreach my $type (
									sort {$a cmp $b} keys %{$strikes{$judge_id}{"entries"}{$entry_id}}
								) {
</%perl>
									<div class="full marno padless">
										<span class="third semibold bluetext nospace">
											<% ucfirst($type) %>
										</span>
										<span class="twothirds nospace">
											<% $strikes{$judge_id}{"entries"}{$entry_id}{$type} %>
										</span>
									</div>
<%perl>
								}
							}

							foreach my $strike_id (
								sort keys %{$strikes{$judge_id}{"times"}}
							) {
</%perl>
								<div class="full marno padless">
									<span class="fifth semibold bluetext nospace">
										Time
									</span>
									<span class="fourfifths">
										<& "/funclib/showdt.mas",
											dt     => $strikes{$judge_id}{times}{$strike_id}{"start"},
											tz     => $tz,
											length => "casual"
										&>
									&ndash;
										<& "/funclib/showdt.mas",
											dt     => $strikes{$judge_id}{times}{$strike_id}{"end"},
											tz     => $tz,
											length => "casual"
										&>
									</span>
								</div>
%							}

						</td>
					</tr>
%				}

			</tbody>

		</table>


	</div>

