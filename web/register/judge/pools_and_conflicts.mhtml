<%args>
	$tourn
	$tourn_settings
	$category_id => undef
</%args>
<%init>

	my $category = Tab::Category->retrieve($category_id);

	unless ($category && $category->tourn == $tourn->id) {
		$m->comp("/funclib/abort.mas",
			message => "No category found with ID $category_id in tournament ".$tourn->name
		);
	}

	my $dbh = Tab::DBI->db_Main();

	my $sth = $dbh->prepare("
		select
			judge.id, judge.active,
			judge.first, judge.middle, judge.last,
			notes.value_text notes,
			person.id person, person.email, person.phone,
			rating_tier.name rating, tab_rating.value tab_rating
		from judge
			left join person on judge.person = person.id
			left join judge_setting notes on notes.judge = judge.id and notes.tag = 'notes'
			left join judge_setting tab_rating on tab_rating.judge = judge.id and tab_rating.tag = 'tab_rating'
			left join rating on rating.judge = judge.id and rating.type = 'coach'
			left join rating_tier on rating.rating_tier = rating_tier.id
		where judge.category = ?
			order by judge.last
	");

	my $conflict_sth = $dbh->prepare("
		select
			judge.id,
			strike.id strike,
			strike.region region_id, region.name region_name,
			strike.school school_id, school.name school_name

		from (judge, strike)
			left join region on strike.region = region.id
			left join school on strike.school = school.id

		where judge.category = ?
			and judge.id = strike.judge
			and strike.type in ('region', 'school')
		order by judge.id
	");

	my $pool_sth = $dbh->prepare("
		select
			judge.id, jpool.id jpool, jpool.name jpool_name

		from judge, jpool_judge jpj, jpool

		where judge.category = ?
			and judge.id = jpj.judge
			and jpj.jpool = jpool.id
			and exists (
				select jps.id
				from jpool_setting jps
				where jps.jpool = jpool.id
				and jps.tag IN ('registrant', 'signups')
			)
		order by judge.id
	");

	$sth->execute($category->id);
	$conflict_sth->execute($category->id);
	$pool_sth->execute($category->id);

	my $judge_refs = $sth->fetchall_hash();

	my $conflict_refs = $conflict_sth->fetchall_hash();
	my $pool_refs = $pool_sth->fetchall_hash();

	my %conflicts;
	my %pools;

	foreach my $conflict (@{$conflict_refs}) {
		if ($conflict->{"region_name"}) {
			push @{$conflicts{$conflict->{"id"}}}, $conflict->{"region_name"};
		} elsif ($conflict->{"school_name"}) {
			push @{$conflicts{$conflict->{"id"}}}, $conflict->{"school_name"};
		}
	}

	foreach my $pool (@{$pool_refs}) {
		my ($name, $rest) = split(/\:/, $pool->{"jpool_name"});
		$name =~ s/Round/Rnd/g;
		$pools{$pool->{"jpool"}}{"name"} = $name;
		$pools{$pool->{"jpool"}}{"judge"}{$pool->{"id"}}++;
	}

</%init>

	<&
		"menu.mas",
		tourn_settings => $tourn_settings,
		tourn          => $tourn,
		whoami         => "pools_conflicts",
		category       => $category
	&>

	<div class="main">

		<& "/funclib/tablesorter.mas", table => "pools_and_conflicts" &>

		<span class="threequarters nospace">
			<h4><% $category->name %></h4>
		</span>
		<span class="quarter rightalign" id="pools_and_conflicts_buttonarea">
		</span>

		<table id ="pools_and_conflicts">

			<thead>
				<tr class="yellowrow smallish">
					<th>
						First
					</th>

					<th>
						Middle
					</th>

					<th>
						Last
					</th>

					<th>
						Role
					</th>

					<th>
						Rating
					</th>

					<th>
						Affiliation
					</th>

					<th>
						Email
					</th>

					<th>
						Phone
					</th>

%					foreach  my $pool (sort {$a <=> $b } keys %pools) {
						<th>
							<% $pools{$pool}{"name"} %>
						</th>
%					}

					<th>
						Conflicts
					</th>
				</tr>
			</thead>

			<tbody>
%				foreach my $judge (@{$judge_refs}) {

					<tr class="smallish">
						<td>
							<% $judge->{"first"} %>
						</td>
						<td>
							<% $judge->{"middle"} %>
						</td>
						<td>
							<% $judge->{"last"} %>
						</td>

						<td class="centeralign">
							<% $judge->{"rating"} %>
						</td>

						<td class="centeralign">
							<% $judge->{"tab_rating"} %>
						</td>
						<td>
							<% $judge->{"notes"} %>
						</td>
						<td>
							<% $judge->{"email"} %>
						</td>
						<td>
							<% Tab::phoneme($judge->{"phone"}) %>
						</td>

%						foreach  my $pool (sort {$a <=> $b } keys %pools) {
							<td class="centeralign">
								<% $pools{$pool}{"judge"}{$judge->{"id"}} ? "Y" : "" %>
							</td>
%						}

						<td>
%							if ($conflicts{$judge->{"id"}}) {
%								foreach my $conflict (sort {$a cmp $b } @{$conflicts{$judge->{"id"}}}) {
									<% $conflict %><br />
%								}
%							}
						</td>
					</tr>
%				}
			</tbody>
		</table>

	</div>

