<%args>
	$person
	$panel_id => undef
	$judge_id => undef
</%args>
<%init>

    my $panel = Tab::Panel->retrieve($panel_id);
    my $judge = Tab::Judge->retrieve($judge_id);

    unless ($panel && $judge) {
        my $err = "No ballots found for that judge and that panel.";
        $m->redirect("../index.mhtml?err=$err");
    }

    my @students = $m->comp('/funclib/panel_students.mas', panel => $panel);

	my %student_entry = ();
	foreach my $student (@students) {
		$student_entry{$student->id} = $student->entry;
	}

	my $round = $panel->round;
	my $event = $round->event;

    my @ballots = Tab::Ballot->search( judge => $judge->id, panel => $panel->id);
	my @scores = $m->comp("/funclib/panel_scores.mas", panel => $panel, judge => $judge);

	my %student_ballot = ();
	foreach my $student (@students) {

		my $mine;

		foreach my $ballot (@ballots) {
			$mine = $ballot if $ballot->entry->id == $student_entry{$student->id};
			last if $mine;
		}

		$student_ballot{$student->id} = $mine->id;
	}

    my $type = $event->type;
    my $tiebreaks = $round->protocol;

    my $tourn = $judge->category->tourn;

	my %tb_types = $m->comp('/funclib/tiebreak_types.mas', round => $round);

    my $max_points = $event->setting("max_points");
    my $min_points = $event->setting("min_points");
	$min_points = 0 unless $min_points;

    my $point_increments = $event->setting("point_increments");
    my $point_ties = $event->setting("point_ties");

	my $errs;

	my %student_values = ();
	my %entry_values = ();

	my %ballot_bye;
	my %ballot_forfeit;

	foreach my $ballot (@ballots) {
		$ballot_bye{$ballot->id}++ if $ARGS{"bye_".$ballot->id};
		$ballot_forfeit{$ballot->id}++ if $ARGS{"forfeit_".$ballot->id};
		$errs = "Error type: HYC+EI2.  (Have Your Cake And Eat It Too).  <br /> You assigned ".$ballot->entry->code." a bye and a forfeit.  One or the other, plz." if $ballot_bye{$ballot->id} && $ballot_forfeit{$ballot->id};;
	}

	if ($tb_types{"rank"}) {

		my %rank_taken;
		my %rank_points;

		foreach my $student (@students) {

			next if ($ballot_bye{$student_ballot{$student->id}});
			next if ($ballot_forfeit{$student_ballot{$student->id}});

			my $rank = $ARGS{$student->id."_ranks"};
			$errs .= "<p>Rank missing</p>" unless $rank;
			next unless $rank;
			$errs .= "<p>Rank must be a number</p>" unless ($rank eq $rank+0);
			$errs .= "<p>You have repeated the rank $rank.  All ranks must be unique.</p>" if $rank_taken{$rank};
			$errs .= "<p>Rank $rank is outside of range.</p>" if $rank < 1 || $rank > scalar @students;

			$rank_taken{$rank}++;
			$student_values{$student->id."_rank"} = $rank;

			if ($tb_types{"point"}) {
				$rank_points{$rank} = $ARGS{$student->id."_points"};
			}

		}

		my $current;

		foreach my $rk (reverse sort keys %rank_points) {
			$errs .= "<p>Student ranked $rk has worse speaks than a lower ranked student (".$rank_points{$rk}." vs ".$current.")</p>" if $rank_points{$rk} < $current;
			$current = $rank_points{$rk};
		}

	}

	my %entry_points = ();

	if ($tb_types{"point"}) {

		my %points_taken;

		foreach my $student (@students) {

			my $points = $ARGS{$student->id."_points"};

			next if ($ballot_bye{$student_ballot{$student->id}}) && not defined $points;
			next if ($ballot_forfeit{$student_ballot{$student->id}}) && not defined $points;

			$errs .= "<p>Points missing</p>" if not defined $points;
			next if not defined $points;

			if ($points > 0) {

				$points = int($points + 0.5) if $point_increments eq "whole";

				if ($point_increments eq "tenths") {
					$points = $points * 10;
					$points = int($points + 0.5);
					$points = $points/10;
				}

				if ($point_increments eq "half") {
					my $rounded_points = $points + .5;
					$rounded_points = $rounded_points - ($points % .5);
					$points = $rounded_points;
				}
			}

			unless ($points == 0 || $points eq $points + 0) {
				$errs .= "<p>$points Points must be a number</p>";
			}

			if ($points_taken{$points} && $point_ties < 1 && $points > 0) {
				$errs .= "<p>Tied points forbidden: you have two speakers with points $points </p>";
			}

			if ($points < $min_points || $points > $max_points) {
				$errs .= "<p>Points $points are outside of range</p>";
			}

			$points_taken{$points}++;
			$student_values{$student->id."_points"} = $points;
			$entry_points{$student_entry{$student->id}} += $points;

		}
	}

	my $winner;

	if ($tb_types{"winloss"})  {

		my $byes;

		foreach my $ballot (@ballots) {
			$byes++ if ($ballot_bye{$ballot->id} || $ballot_forfeit{$ballot->id});
		}

		my $winning_ballot = $ARGS{"winner"};
		$winner = Tab::Ballot->retrieve($winning_ballot);

		$errs .= "<p>You didn't choose a winner.  There are no ties in debate, though there are sometimes tears.  Be strong.</p>" unless $winner || $byes;

		if ($winner) {
			my $winner_points = $entry_points{$winner->entry->id};
			foreach my $ballot (@ballots) {
				my $bal_points = $entry_points{$ballot->entry->id};
				$errs .= "<p>Low point wins are not allowed by this tournament</p>" if $winner_points < $bal_points && $event->setting("no_lpw");
			}
		}
	}

	if ($errs) {

		$m->redirect("../index.mhtml?timeslot_id=".$round->timeslot->id."&judge_id=$judge_id&flight=".$panel->flight."&errs=$errs&err=Trouble in Paradise!");

	} else {

		my $ballot_err;

		my $audit = 0;
		$audit = 1 if $event->category->setting("audit_method") eq "none";

		if (@scores) {

			my %student_rank;
			my %student_points;
			my $current_winner;
			my $current_loser;

			foreach my $bv (@scores) {

				if ($bv->tag eq "rank") {
					$student_rank{$bv->student->id} = $bv->value;
				} elsif ($bv->tag eq "point") {
					$student_points{$bv->student->id} = $bv->value;
				} elsif ($bv->tag eq "winloss") {
					if ($bv->value == 1) {
						$current_winner = $bv->ballot->id;
					} else {
						$current_loser = $bv->ballot->id;
					}
				}
			}

			foreach my $ballot (@ballots) {
				$ballot_err = "Byes differed for results <br />" if $ballot->bye != $ballot_bye{$ballot->id};
				$ballot_err = "Forfeits differed for results <br />" if $ballot->forfeit != $ballot_forfeit{$ballot->id};
			}

			if ($tb_types{"rank"}) {
				foreach my $student (@students) {
					next if $student_rank{$student->id} == $student_values{$student->id."_rank"};
					$ballot_err .= "Ranks for ".$student->id." differed from first entry ".$student_values{$student->id."_rank"}." vs ". $student_rank{$student->id} ."<br />";
				}
			}

			if ($tb_types{"point"}) {
				foreach my $student (@students) {
					next if $student_points{$student->id} == $student_values{$student->id."_points"}
						&& length($student_points{$student->id}) == length($student_values{$student->id."_points"});

					$ballot_err .= "Points for ".$student->last." differed from first entry ";
					$ballot_err .= "(".$student_values{$student->id."_points"}." vs ".$student_points{$student->id}.") <br />";
				}
			}

			if ($tb_types{"winloss"}) {
				$ballot_err .= "Winner differed from first entry." if $winner && $current_winner != $winner->id;
			}

			$audit = 2 unless ($ballot_err);

		}

		unless ($audit == 2) {

			#Clear the old cruft

			Tab::Score->set_sql( clear_cruft => "
				delete score.*
				from score, ballot
				where ballot.panel = ?
				and ballot.judge = ?
				and (score.tag = 'rank' or score.tag = 'point' and score.tag = 'winloss')
				and ballot.id = score.ballot");

			Tab::Score->sql_clear_cruft->execute($panel->id, $judge->id);

		}

		foreach my $ballot (@ballots) {

			$ballot->audit(1) if $audit > 0;
			$ballot->entered_by($person->id);

			$ballot->bye($ballot_bye{$ballot->id});
			$ballot->forfeit($ballot_forfeit{$ballot->id});

			$ballot->update;

			unless ($audit == 2) {

				if ($tb_types{"winloss"}) {

					my $tigers_blood = 0;
					$tigers_blood++ if $winner && $winner->id == $ballot->id;

					undef $tigers_blood if $ballot->forfeit;
					$tigers_blood++ if $ballot->bye;

					Tab::Score->create({
						ballot  => $ballot->id,
						value   => $tigers_blood,
						tag     => "winloss",
						student => 0
					});

				}

				if ($tb_types{"point"}) {

					foreach my $student (@students) {

						next unless $student_entry{$student->id} == $ballot->entry->id;
						next unless length($student_values{$student->id."_points"}) > 0;

						my $bv = Tab::Score->create({
							ballot  => $ballot->id,
							student => $student->id,
							tag     => "point",
							value   => $student_values{$student->id."_points"}
						});

					}

				}

				if ($tb_types{"rank"}) {

					foreach my $student (@students) {

						next unless $student_entry{$student->id} == $ballot->entry->id;
						next unless defined $student_values{$student->id."_points"};

						Tab::Score->create({
							ballot  => $ballot->id,
							student => $student->id,
							tag     => "rank",
							value   => $student_values{$student->id."_rank"}
						});

					}
				}
			}
		}

		my $yellowmsg;
		my $greenmsg;
		my $err;

		if ($ballot_err) {
			$err = "Trouble in paradise!";
		} else {
			$yellowmsg = "Ballot first entry complete.  Ready for a doublecheck" unless $audit > 0;
			$greenmsg = "Ballot entry complete." if $audit > 0;
		}

		$m->redirect("../index.mhtml?timeslot_id=".$round->timeslot->id."&greenmsg=$greenmsg&yellowmsg=$yellowmsg&err=$err&ballot_err=$ballot_err");

	}

</%init>
