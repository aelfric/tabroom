<%args>
	$session
	$tourn
	$person
	$tourn_settings
	$person_settings
	$perms => undef
</%args>
<%init>

	unless ($session && $session->person) {
		my $msg = "You no longer have an active login session.  Please log in again";
		$m->redirect("/index/index.mhtml?msg=$msg");
	}

	my $dbh = Tab::DBI->db_Main();

	return unless $perms->{"tourn"}{$tourn->id} || $person->site_admin;

	my %admin_events;
	my $yo_admin;

	if ($perms->{"by_event"}) {
		foreach my $event_id (keys %{$perms->{"details"}}) {
			if ($perms->{"details"}{$event_id} eq "tabber") {
				$admin_events{$event_id}++;
				$yo_admin++;
			}
		}
	}

	if (
		($perms->{"checker"} || ($perms->{"by_event"} && (not defined $yo_admin)))
		&& (not defined $person->site_admin)
		&& (not defined $person_settings->{"nsda_admin"})
	) {

</%init>

		<ul id="nav">
			<li class="top">
				<a class="top_link wide" href="/tabbing/status/status.mhtml">
					<span class="down">
						Round Starts
					</span>
				</a>
			</li>

			<li class="top">
				<a class="top_link wide" href="/tabbing/status/dashboard.mhtml">
					<span class="down">
						Event Status
					</span>
				</a>
			</li>

			<li class="top">
				<a class="top_link wide" href="/tabbing/entry/index.mhtml">
					<span class="down">
						Ballot Entry
					</span>
				</a>
			</li>

			<li class="top">
				<a class="top_link wide" href="/panel/judge/activate.mhtml">
					<span class="down">
						Judge Roll
					</span>
				</a>
			</li>

			<li class="top">
				<a class="top_link wide" href="/panel/manipulate/entry_activate.mhtml">
					<span class="down">
						Entry Roll
					</span>
				</a>
			</li>
		</ul>

<%perl>

		return;
	}

	my $full_permissions++ if $perms->{"owner"};
	$full_permissions++ if $perms->{"tabber"};

	my $nsda_points;

	# Get the listing of events I want to access

	my %events;

	my $sth = $dbh->prepare("
		select
			event.id, event.abbr, event.name, event.type, event.category,
			nsda_event_category.value,
			supp.value, conn.value, stefan.value,
			leadership.value, student_vote_protocol.value, student_online_ballots.value,
			weekend.id, weekend.start, weekend.end

		from event

			left join event_setting nsda_event_category
				on nsda_event_category.tag = 'nsda_event_category'
				and nsda_event_category.event = event.id

			left join event_setting supp
				on supp.tag = 'supp'
				and supp.event = event.id

			left join event_setting conn
				on conn.tag = 'conn'
				and conn.event = event.id

			left join event_setting stefan
				on stefan.tag = 'stefan'
				and stefan.event = event.id

			left join event_setting leadership
				on leadership.tag = 'leadership'
				and leadership.event = event.id

			left join event_setting student_vote_protocol
				on student_vote_protocol.tag = 'student_vote_protocol'
				and student_vote_protocol.event = event.id

			left join event_setting student_online_ballots
				on student_online_ballots.tag = 'student_online_ballots'
				and student_online_ballots.event = event.id

			left join event_setting weekend_set
				on weekend_set.tag = 'weekend'
				and weekend_set.event = event.id

			left join weekend
				on weekend.tourn = event.tourn
				and weekend.id = weekend_set.value

		where event.tourn = ?
			and event.type != 'attendee'

		and exists (
			select round.id
			from round
			where round.event = event.id
		)
	");

	$sth->execute($tourn->id);

	my $sv;

	while (
		my (
			$event_id, $event_abbr, $event_name, $event_type, $category_id,
			$nsda_category, $supp, $conn, $stefan,
			$leadership, $student_vote, $student_ballot,
			$weekend_id, $weekend_start, $weekend_end
		) = $sth->fetchrow_array()
	) {

		$events{$event_id}{"name"} = $event_name;
		$events{$event_id}{"abbr"} = $event_abbr;
		$events{$event_id}{"type"} = $event_type;
		$events{$event_id}{"type"} = "debate" if $event_type eq "wsdc";

		$nsda_category = 200 if $nsda_category > 200; # Don't shuffle sort speech
		$events{$event_id}{"code"}     = $nsda_category;
		$events{$event_id}{"category"} = $category_id;

		$events{$event_id}{"supp"}   = $supp;
		$events{$event_id}{"conn"}   = $conn;
		$events{$event_id}{"stefan"} = $stefan;

		if ($weekend_id) {

			$events{$event_id}{"weekend_start"} = eval {
				my $dt = DateTime::Format::MySQL->parse_datetime($weekend_start);
				$dt->set_time_zone("UTC");
				return $dt;
			};

			$events{$event_id}{"weekend_end"} = eval {
				my $dt = DateTime::Format::MySQL->parse_datetime($weekend_end);
				$dt->set_time_zone("UTC");
				return $dt;
			};
		}

		if ($leadership || $student_vote || $student_ballot) {
			$sv++;
		}
	}

	my %ok;

	if (
		$perms->{"owner"}
		|| $perms->{"tabber"}
		|| $person->site_admin
		|| $person_settings->{"nsda_admin"}
	) {

		my $sth = $dbh->prepare("
			select school.id
				from school, chapter, entry, ballot
			where school.tourn = ?
				and school.chapter = chapter.id
				and chapter.nsda > 0
				and school.id = entry.school
				and entry.id = ballot.entry
			limit 1
		");

		$sth->execute($tourn->id);

		if ($sth->fetchrow_array()) {
			$nsda_points++;
		}

		foreach my $event_id (keys %events) {
			$ok{$event_id}++;
		}
	}

	if ($perms->{"by_event"}) {
		foreach my $event_id (keys %admin_events) {
			$ok{$event_id}++;
		}
	} elsif ($full_permissions) {
		foreach my $event_id (keys %events) {
			$ok{$event_id}++;
		}
	}

	if ($tourn_settings->{"nsda_district"}) {

		unless (
			$perms->{"owner"}
			|| $person->site_admin
			|| $person_settings->{"nsda_admin"}
		) {

			my $now = DateTime->now();
			my $then = $now->clone();
			$then->add(days => 7);
			$now->subtract(days => 7);

			foreach my $event_id (keys %ok) {
				unless (
					$events{$event_id}{"weekend_start"} < $then
					&& $events{$event_id}{"weekend_end"} > $now
				) {
					delete $ok{$event_id};
				}
			}
		}
	}

	unless (keys %ok || $full_permissions) {
		return;
	}

	foreach my $event_id (keys %events) {
		next if $ok{$event_id};
		delete $events{$event_id};
	}

	my @events = sort {
		$events{$a}{"stefan"} cmp $events{$b}{"stefan"}
		|| $events{$a}{"conn"} cmp $events{$b}{"conn"}
		|| $events{$a}{"supp"} cmp $events{$b}{"supp"}
		|| $events{$a}{"type"} cmp $events{$b}{"type"}
		|| $events{$a}{"code"} cmp $events{$b}{"code"}
		|| $events{$a}{"abbr"} cmp $events{$b}{"abbr"}
		|| $events{$a}{"name"} cmp $events{$b}{"name"}
	} keys %events;

	my %menu = ();

	if ($full_permissions || keys %admin_events) {

		$menu{"settings"} = ({
			order => 1,
			links => {
				1 => {name => "Tournament"        , url => "/setup/tourn/main.mhtml"}      ,
				2 => {name => "Rules & Results"   , url => "/setup/rules/tiebreaks.mhtml"} ,
				3 => {name => "Judges"            , url => "/setup/judges/edit.mhtml"}     ,
				4 => {name => "Events"            , url => "/setup/events/edit.mhtml"}     ,
				5 => {name => "Schedule"          , url => "/setup/schedule/sked.mhtml"}   ,
				6 => {name => "Sites &amp; Rooms" , url => "/setup/rooms/list.mhtml"}      ,
				7 => {name => "Money"             , url => "/setup/money/edit.mhtml"}      ,
				8 => {name => "Website"           , url => "/setup/web/edit.mhtml"}        ,
			}
		});

		if (keys %admin_events && (not defined $full_permissions)) {
			delete $menu{"settings"}{"links"}{1};
			delete $menu{"settings"}{"links"}{2};
			delete $menu{"settings"}{"links"}{3};
			delete $menu{"settings"}{"links"}{6};
			delete $menu{"settings"}{"links"}{7};
			delete $menu{"settings"}{"links"}{8};

		}

		delete $menu{"settings"}{"links"}{2}
			if ($tourn_settings->{"nsda_district"})
			&& (not defined $person_settings->{"nsda_admin"})
			&& ($person->site_admin != 1);
	}

	if ($full_permissions || keys %admin_events) {

		$menu{"entries"} = ({
			order => 2,
			links => {
				1 => {name => "Schools"    , url => "/register/index.mhtml"}         ,
				2 => {name => "Events"     , url => "/register/event/index.mhtml"}   ,
				3 => {name => "Judges"     , url => "/register/judge/index.mhtml"}   ,
				4 => {name => "Reports"    , url => "/register/reports/index.mhtml"} ,
				5 => {name => "Change Log" , url => "/register/changes/index.mhtml"} ,
				6 => {name => "Data"       , url => "/register/data/index.mhtml"}    ,
				7 => {name => "Emails"     , url => "/register/emails/index.mhtml"}
			}
		});


		if (keys %admin_events && (not defined $full_permissions)) {
			delete $menu{"entries"}{"links"}{1};
			delete $menu{"entries"}{"links"}{4};
			delete $menu{"entries"}{"links"}{6};
			delete $menu{"entries"}{"links"}{8};
		} elsif ($full_permissions) {
			if ($tourn_settings->{"ncfl"}) {
				$menu{"entries"}{"links"}{0} = ({
					name => "Dioceses",
					url  => "/register/region/index.mhtml"
				});
			} elsif ($tourn_settings->{"nsda_nats"}) {
				$menu{"entries"}{"links"}{0} = ({
					name => "Districts",
					url  => "/register/district/index.mhtml"
				});
			}
			if ($tourn_settings->{"housing"}) {
				$menu{"entries"}{"links"}{3.5} = ({
					name => "Housing",
					url  => "/register/housing/index.mhtml"
				});
			}
		}
	}

	if ($full_permissions || keys %admin_events) {

		if ($full_permissions || keys %admin_events) {

			$menu{"paneling"} = ({
				order => 3,
				links => {
					1 => {name => "Reports"     , url => "/panel/report/index.mhtml"}  ,
					2 => {name => "Rounds"      , url => "/panel/round/index.mhtml"}    ,
					3 => {name => "Judges"      , url => "/panel/judge/index.mhtml"}   ,
					4 => {name => "Rooms"       , url => "/panel/room/index.mhtml"}    ,
					5 => {name => "Web Publish" , url => "/panel/publish/index.mhtml"} ,
				}
			});

			if ($tourn_settings->{"nsda_district"}
				&& (not defined $person_settings->{"nsda_admin"})
				&& (not defined $person->site_admin)
			) {
				delete $menu{"paneling"}{"links"}{2};
			}
		}

		$menu{"tabbing"} = ({
			order => 5,
			links => {
				1 => {name => "Enter Ballots" , url => "/tabbing/entry/index.mhtml"}        ,
				2 => {name => "Status"        , url => "/tabbing/status/dashboard.mhtml"}   ,
				3 => {name => "Codebreaker"   , url => "/tabbing/report/codebreaker.mhtml"} ,
				4 => {name => "Breaks"        , url => "/tabbing/break/index.mhtml"}        ,
				5 => {name => "Entry Cards"   , url => "/tabbing/entry/card.mhtml"}         ,
				6 => {name => "Sweepstakes"   , url => "/tabbing/entry/sweeps.mhtml"}       ,
			}
		});

		unless ($full_permissions || keys %admin_events) {
			delete $menu{"tabbing"}{"links"}{4};
			delete $menu{"tabbing"}{"links"}{5};
			delete $menu{"tabbing"}{"links"}{6};
		}

		if ($sv) {
			$menu{"tabbing"}{"links"}{7} = ({
				name => "Student Vote",
				url  => "/tabbing/entry/student_vote.mhtml"
			});
		}

		if ($full_permissions || keys %admin_events) {

			$menu{"results"} = ({
				order => 6,
				links => {
					1 => {name => "Event Display" , url => "/tabbing/results/index.mhtml"}   ,
					5 => {name => "Reports"       , url => "/tabbing/report/index.mhtml"} ,
					6 => {name => "Web Publish"   , url => "/tabbing/publish/index.mhtml"}   ,
				}
			});


			if ($tourn_settings->{"nsda_district"}) {

				$menu{"results"}{"links"}{2} = ({
					name => "Qualifiers",
					url  => "/tabbing/results/nsda_qualifiers.mhtml"
				});

				$menu{"results"}{"links"}{3} = ({
					name => "Sweepstakes",
					url  => "/tabbing/results/nsda_sweepstakes.mhtml"
				});

				if ($tourn_settings->{"nsda_district_questions"}{"nsda_tabbing_software"} eq "speechwire") {
					$menu{"results"}{"links"}{4} = ({
						name => "SW Data Upload",
						url  => "/tabbing/publish/swdistrict.mhtml"
					});
				}
				delete $menu{"results"}{"links"}{5};

			} elsif ($tourn_settings->{"nsda_nats"}) {

				$menu{"results"}{"links"}{2} = ({
					name => "Placement",
					url  => "/tabbing/results/nsda_qualifiers.mhtml"
				});

			} else {
			}

			if ($nsda_points) {
				$menu{"results"}{"links"}{7} = ({
					name => "NSDA Points",
					url  => "/tabbing/results/nsda_points.mhtml"
				});
			}

		}
	}

	my $last_type;
	my $last_supp = 0;
	my $last_conn = 0;
	my $counter;

	$menu{"schemats"} = ({
		order => 4,
		class => "narrow"
	});

	if (@events) {

		foreach my $event_id (@events) {

			my $class;
			$counter++;

			$last_type = $events{$event_id}{type} unless $last_type;
			$class = "martop" if $events{$event_id}{"type"} ne $last_type;
			$last_type = $events{$event_id}{"type"};

			if ($tourn_settings->{"nsda_nats"} && $events{$event_id}{"type"} eq 'speech') {

				if ($events{$event_id}{"supp"} && ($last_supp < 1)) {
					$class = "martop";
					$last_supp++;
				}

				if ($events{$event_id}{"conn"} && ($last_conn < 1)) {
					$class = "martop";
					$last_conn++;
				}
			}

			$menu{"schemats"}{"links"}{$counter}{"url"} = "/panel/schemat/show.mhtml?event_id=".$event_id;
			$menu{"schemats"}{"links"}{$counter}{"name"} = $events{$event_id}{"abbr"};
			$menu{"schemats"}{"links"}{$counter}{"title"} = $events{$event_id}{"name"};
			$menu{"schemats"}{"links"}{$counter}{"class"} = $class;

		}
	}

	my $tid = "?tourn_id=".$tourn->id;

</%perl>

	<ul id="nav">

%		foreach my $column (sort {$menu{$a}{"order"} <=> $menu{$b}{"order"}} keys %menu) {
			<li class="top">

				<a class="top_link">
					<span class="down"><% ucfirst($column) %> </span>
				</a>

	        	<ul class="sub <% $menu{$column}{"class"} %> ">
%				foreach my $link (sort {$a <=> $b} keys %{$menu{$column}{"links"}}) {
					<li>
						<a
						class="<% $menu{$column}{"links"}{$link}{"class"} %>"
						title="<% $menu{$column}{"links"}{$link}{"title"} %>"
						href="<% $menu{$column}{"links"}{$link}{"url"} %><% $column ne "schemats" ? $tid : "" %>"
						><% $menu{$column}{"links"}{$link}{"name"} %></a>
					</li>
%				}
				</ul>
			</li>
%		}
	</ul>

