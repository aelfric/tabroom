<%args>
	$tourn
	$tourn_settings
	$perms
	$session
	$defaults      => undef
	$jpool_id      => undef
	$only_category => undef
	$category_id   => undef
</%args>
<%init>


    my (
        $category, $categoriesref,
        $jpool, $jpoolsref, $jpool_settingsref,
        $parent, $parents_ref,
		$defs, $event_limit, $cat_limit
    ) = $m->comp("pools.mas",
        tourn       => $tourn,
        perms       => $perms,
        defaults    => $defaults,
        session     => $session,
        nsda_nats   => $tourn_settings->{"nsda_nats"},
        jpool_id    => $jpool_id,
        parent      => $ARGS{"parent"},
        category_id => $category_id,
    );

	my $nats_category = $category->setting("nats_category") if $category;

	my $dbh = Tab::DBI->db_Main();
	my $e_limit;

	if ($event_limit) {
		$e_limit = " and event.id IN ($event_limit) ";
	}

	my $sth = $dbh->prepare("
		select
			event.id event, event.name, event.abbr, event.type,
			round.id round, round.name round_name, round.label, round.type round_type,
			supp.value supp
		from (round, event)

			left join event_setting supp
				on supp.event = event.id
				and supp.tag = 'supp'

		where event.category = ?
			and event.id = round.event
			$e_limit
		order by event.type, supp.value, event.abbr, round.name
	");

	$sth->execute($category->id);
	my $rounds = $sth->fetchall_hash();

</%init>

	<& "menu.mas",
		tourn             => $tourn,
		perms             => $perms,
		category          => $category,
		jpool             => $jpool,
		nats_category     => $nats_category,
		whoami            => "availability",
		only_category     => $only_category,
		tourn_settings    => $tourn_settings,
		parent            => $parent,
		parents_ref       => $parents_ref,
		categoriesref     => $categoriesref,
		jpoolsref         => $jpoolsref,
		jpool_settingsref => $jpool_settingsref,
	&>

	<div class="main">

%		if ($category) {

			<h2><% $category->name %></h2>

				<& "tabbar.mas",
					perms    => $perms,
					category => $category,
					jpool    => $jpool,
					nsda     => $tourn_settings->{'nsda_nats'},
					whoami   => "availability"
				&>
			<script>

				function showEvent() {
					$(".events").addClass("hidden");
					const eventId = $('#event').val();
					$(`.${eventId}`).removeClass('hidden');
					console.log(`remove hidden from .${eventId}`);
				}

				$(document).ready( () => {
					showEvent();
				});

			</script>

			<div class="nospace padtopmore blueborderbottom padbottommore">
				<span class="half">
					<h4>
						Judges Available in <% $category->abbr %>
					</h4>
				</span>

				<span class="half rightalign">
					<select
						id       = "event"
						onChange = "showEvent();"
					>
<%perl>
					my %done;

					foreach my $round (
						sort {
							$a->{type} cmp $b->{type}
							|| $a->{supp} cmp $b->{supp}
							|| $a->{abbr} cmp $b->{abbr}
							|| $a->{round_name} <=> $b->{round_name}
						} @{$rounds}
					) {

						next if $done{$round->{event}}++;
</%perl>
						<option
							value="<% $round->{event} %>"
						><% $round->{name} %></option>
%					}

					</select>
				</span>
			</div>

<%perl>
			my $count;
			my $current_event;

			foreach my $round (
				sort {
					$a->{type} cmp $b->{type}
					|| $a->{supp} cmp $b->{supp}
					|| $a->{abbr} cmp $b->{abbr}
					|| $a->{round_name} <=> $b->{round_name}
				} @{$rounds}
			) {

				if ($round->{event} != $current_event) {

					if ($current_event) {
						$m->print('</div>');
					}
					$current_event = $round->{event};
</%perl>
					<div class="top nospace bluebordertop events <% $round->{event} %>">
						<h5 class="marleft semibold bluetext"><% $round->{abbr} %> <% $round->{name} %></h5>
%				}

				<div class="padvertless marno row">
					<span class="third">
						<span class="quarterspacer"></span>
						<% $round->{label} ? $round->{label} : "Round ".$round->{round_name} %>
					</span>

					<span class="third">
						<% ucfirst($round->{round_type}) %>
					</span>

					<span class="third centeralign semibold bluetext">
						<% $m->comp("/funclib/round_available_judges.mas",
							round    => $round->{round},
							category => $category->id
						) %>
					</span>
				</div>
%			}
			</div>
%		}

	</div>
